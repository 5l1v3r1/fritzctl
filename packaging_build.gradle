apply plugin: 'nebula.ospackage'
apply plugin: 'nebula.deb'

group = 'fritzctl'
version = System.getenv("FRITZCTL_VERSION") ?: "0.0.1unknown"
description = 'AVM FRITZ!Box client. See https://github.com/bpicode/fritzctl for further details.'

buildscript {

    dependencies {
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:4.1.0'
    }

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

buildDeb {

    os = LINUX
    arch = System.getenv("GOARCH") ?: "amd64"
    packager = "bpicode"
    summary = 'AVM FRITZ!Box client'
    license = 'MIT'
    url = "https://github.com/bpicode/fritzctl"
    maintainer = packager
    uploaders = packager
    provides = [new com.netflix.gradle.plugins.packaging.Dependency("fritzctl", "")]
    packageGroup = "utils"

    configurationFile('/etc/fritzctl/fritzctl.json')

    from("$System.env.GOPATH" + "/src/github.com/bpicode/fritzctl/fritzctl") {
        into '/usr/bin'
    }

    from("os/config/fritzctl.json") {
        into '/etc/fritzctl'
    }

    from("os/config/fritz.pem") {
        into '/etc/fritzctl'
    }

    configurationFile =
        "/etc/fritzctl/fritzctl.json\n" +
        "/etc/fritzctl/fritz.pem"

    from("os/completion/fritzctl") {
        into '/etc/bash_completion.d'
    }
}

buildRpm {

    os = LINUX
    def goArch = System.getenv("GOARCH") ?: "amd64"
    switch(goArch) {
        case "amd64": 
            arch = X86_64
            break
        case "arm":
            arch = ARM
            break
        default:
            arch = goArch
    }
    packager = "bpicode"
    summary = 'AVM FRITZ!Box client'
    license = 'MIT'
    url = "https://github.com/bpicode/fritzctl"
    maintainer = packager
    uploaders = packager
    provides = [new com.netflix.gradle.plugins.packaging.Dependency("fritzctl", "")]
    packageGroup = "utils"

    configurationFile('/etc/fritzctl/fritzctl.json')

    from("$System.env.GOPATH" + "/src/github.com/bpicode/fritzctl/fritzctl") {
        into '/usr/bin'
    }

    from("os/config/fritzctl.json") {
        fileType CONFIG | NOREPLACE
        into '/etc/fritzctl'
    }

    from("os/config/fritz.pem") {
        fileType CONFIG | NOREPLACE
        into '/etc/fritzctl'
    }

    from("os/completion/fritzctl") {
        into '/etc/bash_completion.d'
    }
}
