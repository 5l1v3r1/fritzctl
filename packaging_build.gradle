//apply plugin: 'nebula.ospackage'
apply plugin: 'nebula.deb'

group = 'fritzctl'
version = System.getenv("FRITZCTL_VERSION") ?: "0.0.1-unknown"
description = 'AVM FRITZ!Box client. See https://github.com/bpicode/fritzctl for further details.'

buildscript {

    dependencies {
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:4.1.0'
    }

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

task buildDeb(type: Deb) {

    os = LINUX
    arch = System.getenv("GOARCH") ?: "amd64"
    packager = "bpicode"
    summary = 'AVM FRITZ!Box client'
    license = 'MIT'
    url = "https://github.com/bpicode/fritzctl"
    provides = [new com.netflix.gradle.plugins.packaging.Dependency("fritzctl", "")]
    maintainer = packager
    uploaders = packager

    configurationFile('/etc/fritzctl/fritzctl.json')

    from("$System.env.GOPATH" + "/src/github.com/bpicode/fritzctl/fritzctl") {
        into '/usr/bin'
    }

    from("os/config/fritzctl.json") {
        fileType CONFIG | NOREPLACE
        into '/etc/fritzctl'
    }

    from("os/completion/fritzctl") {
        into '/etc/bash_completion.d'
    }
}
